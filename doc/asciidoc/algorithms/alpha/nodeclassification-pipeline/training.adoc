[[algorithms-ml-nodeclassification-pipelines-train]]
== Training the pipeline

The train mode, `gds.beta.pipeline.nodeClassification.train`, is responsible for splitting data, feature extraction, model selection, training and storing a model for future use.
Running this mode results in a classification model of type `NodeClassification`, which is then stored in the <<model-catalog-ops, model catalog>>.
The classification model can be <<algorithms-node-classification-pipelines-predict, applied>> to a possibly different graph which classifies nodes.

More precisely, the training proceeds as follows:

. Apply `nodeLabels` and `relationshipType` filters to the graph.
. Apply the node property steps, added according to <<algorithms-ml-nodeclassification-adding-node-properties, Adding node properties>>, on the whole graph.
. Select node properties to be used as features, as specified in <<algorithms-ml-nodeclassification-adding-features, Adding features>>.
. Split the input graph into two parts: the train graph and the test graph. This is described in <<algorithms-ml-nodeclassification-configure-splits, Configuring the node splits>>.
These graphs are internally managed and exist only for the duration of the training.
. Split the nodes in the train graph using stratified k-fold cross-validation.
The number of folds `k` can be configured as described in <<algorithms-ml-nodeclassification-configure-splits, Configuring the node splits>>.
. Each model candidate defined in the <<algorithms-ml-nodeclassification-configure-model-parameters,parameter space>> is trained on each train set and evaluated on the respective validation set for every fold. The training process uses a logistic regression algorithm, and the evaluation uses the specified <<algorithms-ml-nodeclassification-pipeline-metrics,metric>>.
. Choose the best performing model according to the highest average score for the primary metric.
. Retrain the winning model on the entire train graph.
. Evaluate the performance of the winning model on the whole train graph as well as the test graph.
. Retrain the winning model on the entire original graph.
. Register the winning model in the <<model-catalog-ops, Model Catalog>>.

NOTE: The above steps describe what the procedure does logically.
The actual steps as well as their ordering in the implementation may differ.

NOTE: A step can only use node properties that are already present in the input graph or produced by steps, which were added before.

[[algorithms-ml-nodeclassification-pipeline-metrics]]
=== Metrics

The Node Classification model in the Neo4j GDS library supports the following evaluation metrics:

* Global metrics
** `F1_WEIGHTED`
** `F1_MACRO`
** `ACCURACY`

* Per-class metrics
** `F1(class=<number>)` or `F1(class=*)`
** `PRECISION(class=<number>)` or `PRECISION(class=*)`
** `RECALL(class=<number>)` or `RECALL(class=*)`
** `ACCURACY(class=<number>)` or `ACCURACY(class=*)`

The `*` is syntactic sugar for reporting the metric for each class in the graph.
When using a per-class metric, the reported metrics contain keys like for example `ACCURACY_class_1`.

More than one metric can be specified during training but only the first specified -- the `primary` one -- is used for evaluation, the results of all are present in the train results.
The primary metric may not be a `*` expansion due to the ambiguity of which of the expanded metrics should be the `primary` one.

=== Syntax

[.include-with-train]
--
.Run Node Classification in train mode on a named graph:
[source, cypher, role=noplay]
----
CALL gds.beta.pipeline.nodeClassification.train(
  graphName: String,
  configuration: Map
) YIELD
  trainMillis: Integer,
  modelInfo: Map,
  modelSelectionStats: Map,
  configuration: Map
----

include::../../common-configuration/common-parameters-named-graph.adoc[]

.Configuration
[opts="header",cols="1,1,1m,1,4"]
|===
| Name                                                          | Type              | Default | Optional | Description
| pipeline                                                      | String            | n/a     | no       | The name of the pipeline to execute.
| <<common-configuration-node-labels,nodeLabels>>               | List of String    | ['*']   | yes      | Filter the named graph using the given node labels.
| <<common-configuration-relationship-types,relationshipTypes>> | List of String    | ['*']   | yes      | Filter the named graph using the given relationship types.
| <<common-configuration-concurrency,concurrency>>              | Integer           | 4       | yes      | The number of concurrent threads used for running the algorithm.
| targetProperty                                                | String            | n/a     | no       | The class of the node. Must be of type Integer.
| metrics                                                       | List of String    | n/a     | no       | <<algorithms-ml-nodeclassification-pipeline-metrics,Metrics>> used to evaluate the models.
| randomSeed                                                    | Integer           | n/a     | yes      | Seed for the random number generator used during training.
| modelName                                                     | String            | n/a     | no       | The name of the model to train, must not exist in the Model Catalog.
|===

.Results
[opts="header",cols="1,1,6"]
|===
| Name          | Type    | Description
| trainMillis   | Integer | Milliseconds used for training.
| modelInfo               | Map     | Information about the training and the winning model.
| modelSelectionStats     | Map     | Statistics about evaluated metrics for all model candidates.
| configuration | Map     | Configuration used for the train procedure.
|===

The `modelInfo` can also be retrieved at a later time by using the <<catalog-model-list, Model List Procedure>>.
The `modelInfo` return field has the following algorithm-specific subfields:

.Model info fields
[opts="header",cols="1,1,6"]
|===
| Name                    | Type            | Description
| classes                 | List of Integer | Sorted list of class ids which are the distinct values of `targetProperty` over the entire graph.
| bestParameters          | Map             | The model parameters which performed best on average on validation folds according to the primary metric.
| metrics                 | Map             | Map from metric description to evaluated metrics for the winning model over the subsets of the data, see below.
| trainingPipeline        | Map             | The pipeline used for the training.
|===

The structure of `modelInfo` is:

[listing]
----
{
    bestParameters: Map,        // <1>
    trainingPipeline: Map       // <2>
    classes: List of Integer,   // <3>
    metrics: {                  // <4>
        <METRIC_NAME>: {        // <5>
            test: Float,        // <6>
            outerTrain: Float,  // <7>
            train: {           // <8>
                avg: Float,
                max: Float,
                min: Float,
            },
            validation: {      // <9>
                avg: Float,
                max: Float,
                min: Float,
                params: Map
            },
            {
                avg: Float,
                max: Float,
                min: Float,
                params: Map
            },
            ...
            ]
        }
    }
}
----
<1> The best scoring model candidate configuration.
<2> The pipeline used for the training.
<3> Sorted list of class ids which are the distinct values of `targetProperty` over the entire graph.
<4> The `metrics` map contains an entry for each metric description, and the corresponding results for that metric.
<5> A metric name specified in the configuration of the procedure, e.g., `F1_MACRO` or `RECALL(class=4)`.
<6> Numeric value for the evaluation of the winning model on the test set.
<7> Numeric value for the evaluation of the winning model on the outer train set.
<8> The `train` entry summarizes the metric results over the `train` set.
<9> The `validation` entry summarizes the metric results over the `validation` set.
--

[[algorithms-ml-nodeclassification-examples-train]]
=== Example

In this section we will show examples of running a Node Classification training pipeline on a concrete graph.
The intention is to illustrate what the results look like and to provide a guide in how to make use of the model in a real setting.
We will do this on a small graph of a handful of nodes representing houses.
This is an example of Multi-class classification, the `class` node property distinct values determine the number of classes, in this case three (0, 1 and 2).
The example graph looks like this:

image::example-graphs/node_classification.svg[align="center"]

.The following Cypher statement will create the example graph in the Neo4j database:
[source, cypher, role=noplay setup-query, group=nc]
----
CREATE
  (:House {color: 'Gold', sizePerStory: [15.5, 23.6, 33.1], class: 0}),
  (:House {color: 'Red', sizePerStory: [15.5, 23.6, 100.0], class: 0}),
  (:House {color: 'Blue', sizePerStory: [11.3, 35.1, 22.0], class: 0}),
  (:House {color: 'Green', sizePerStory: [23.2, 55.1, 0.0], class: 1}),
  (:House {color: 'Gray', sizePerStory: [34.3, 24.0, 0.0],  class: 1}),
  (:House {color: 'Black', sizePerStory: [71.66, 55.0, 0.0], class: 1}),
  (:House {color: 'White', sizePerStory: [11.1, 111.0, 0.0], class: 1}),
  (:House {color: 'Teal', sizePerStory: [80.8, 0.0, 0.0], class: 2}),
  (:House {color: 'Beige', sizePerStory: [106.2, 0.0, 0.0], class: 2}),
  (:House {color: 'Magenta', sizePerStory: [99.9, 0.0, 0.0], class: 2}),
  (:House {color: 'Purple', sizePerStory: [56.5, 0.0, 0.0], class: 2}),
  (:UnknownHouse {color: 'Pink', sizePerStory: [23.2, 55.1, 56.1]}),
  (:UnknownHouse {color: 'Tan', sizePerStory: [22.32, 102.0, 0.0]}),
  (:UnknownHouse {color: 'Yellow', sizePerStory: [39.0, 0.0, 0.0]});
----

With the graph in Neo4j we can now project it into the graph catalog to prepare it for the pipeline execution.
We do this using a native projection targeting the `House` and `UnknownHouse` labels.
We will also project the `sizeOfStory` property to use as a model feature, and the `class` property to use as a target feature.

include::../../shared/examples-named-native-note.adoc[]

.The following statement will project a graph using a native projection and store it in the graph catalog under the name 'myGraph'.
[source, cypher, role=noplay graph-project-query, group=nc]
----
CALL gds.graph.project('myGraph', {
    House: { properties: ['sizePerStory', 'class'] },
    UnknownHouse: { properties: 'sizePerStory' }
  },
  '*'
)
----

[[algorithms-ml-nodeclassification-examples-train-memory-estimation]]
==== Memory Estimation
:mode: train

include::../../shared/examples-estimate-intro.adoc[]

[role=query-example, group=nc]
--
.The following will estimate the memory requirements for running the algorithm in train mode:
[source, cypher, role=noplay]
----
CALL gds.beta.pipeline.nodeClassification.train.estimate('myGraph', {
  pipeline: 'pipe',
  nodeLabels: ['House'],
  modelName: 'nc-model',
  targetProperty: 'class',
  randomSeed: 2,
  metrics: [ 'F1_WEIGHTED' ]
})
YIELD bytesMin, bytesMax, requiredMemory
----

.Results
[opts="header"]
|===
| bytesMin  | bytesMax  | requiredMemory
| 66786312  | 66818272  | +"[63 MiB ... 63 MiB]"+
|===
--

NOTE: If a node property step does not have an estimation implemented, the step will be ignored in the estimation.


==== Train

In the following examples we will demonstrate running the Node Classification training pipeline on this graph.
We will train a model to predict the class in which a house belongs, based on its `sizePerStory` property.

[[algorithms-ml-nodeclassification-pipeline-examples-train-query]]
[role=query-example, group=nc]
--
.The following will train a model using a pipeline:
[source, cypher, role=noplay]
----
CALL gds.beta.pipeline.nodeClassification.train('myGraph', {
  pipeline: 'pipe',
  nodeLabels: ['House'],
  modelName: 'nc-pipeline-model',
  targetProperty: 'class',
  randomSeed: 42,
  concurrency:1,
  metrics: ['F1_WEIGHTED']
}) YIELD modelInfo
RETURN
  modelInfo.bestParameters AS winningModel,
  modelInfo.metrics.F1_WEIGHTED.train.avg AS avgTrainScore,
  modelInfo.metrics.F1_WEIGHTED.outerTrain AS outerTrainScore,
  modelInfo.metrics.F1_WEIGHTED.test AS testScore
----

.Results
[opts="header", cols="6, 2, 2, 2"]
|===
| winningModel                                                                                                  | avgTrainScore | outerTrainScore    | testScore
| {useBiasFeature=true, maxEpochs=100, minEpochs=1, penalty=0.0625, patience=1, batchSize=100, tolerance=0.001} | 0.999999989939394 | 0.9999999912121211 | 0.9999999850000002
|===

Here we can observe that the model candidate with penalty `0.0625` performed the best in the training phase, with an `F1_WEIGHTED` score nearing 1 over the train graph as well as on the test graph.
This indicates that the model reacted very well to the train graph, and was able to generalize fairly well to unseen data.
Notice that this is just a toy example on a very small graph. In order to achieve a higher test score, we may need to use better features, a larger graph, or different model configuration.
--
